apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def majorVersion = 7
    def minorVersion = 2
    def patchVersion = 0

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        if (file("signing.gradle").exists()) {
            apply from: 'signing.gradle'
        }
    }

    defaultConfig {
        applicationId "com.wire.testinggallery"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName "${majorVersion}.${minorVersion}.${patchVersion}"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    flavorDimensions "default"

    productFlavors {
        qa {
            dimension "default"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:24.1-jre'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.robolectric:robolectric:4.3'
}

//add pretty naming to apk filename
android.applicationVariants.all { variant ->
    def appName = "testing-gallery"

    variant.outputs.each { output ->
        def newApkName
        newApkName = "${appName}-${output.baseName}.apk"
        output.outputFileName = new File("../..", newApkName)
    }
}
